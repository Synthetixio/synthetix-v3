//SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

/**
 * @title Utility library used to represent "decimals" (fixed point numbers) with integers, with two different levels of precision.
 *
 * They are represented by N * UNIT, where UNIT is the number of decimals of precision in the representation.
 *
 * Examples:
 * 1) Given UNIT = 100
 * then if A = 50, A represents the decimal 0.50
 * 2) Given UNIT = 1000000000000000000
 * then if A = 500000000000000000, A represents the decimal 0.500000000000000000
 */
library DecimalMath {
    // Numbers representing 1.0 (normal precision).
    uint256 public constant UNIT = 1e18;
    int256 public constant UNIT_INT = int256(UNIT);
    int128 public constant UNIT_INT128 = int128(UNIT_INT);

    // Numbers representing 1.0 (high precision).
    uint256 public constant UNIT_PRECISE = 1e27;
    int256 public constant UNIT_INT_PRECISE = int256(UNIT_PRECISE);
    int128 public constant UNIT_INT128_PRECISE = int128(UNIT_INT_PRECISE);

    // -----------------------------------------------------------------------
    // -----------------------------------------------------------------------
    // Important!
    //
    // Multiplication and division operations are currently not supported
    // for high precision decimals. Using these operations on them will
    // yield incorrect results and fail silently.
    // -----------------------------------------------------------------------
    // -----------------------------------------------------------------------

    // -----------------
    // uint256
    // -----------------

    /**
     * @dev Multiplies two fixed point numbers.
     *
     * Since the two numbers are assumed to be fixed point numbers,
     * (x * UNIT) * (y * UNIT) = x * y * UNIT ^ 2,
     * the result is divided by UNIT to remove double scaling.
     */
    function mulDecimal(uint256 x, uint256 y) internal pure returns (uint256 z) {
        return (x * y) / UNIT;
    }

    /**
     * @dev Divides two fixed point numbers.
     *
     * Since the two numbers are assumed to be fixed point numbers,
     * (x * UNIT) / (y * UNIT) = x / y (Decimal representation is lost),
     * x is first scaled up to end up with a decimal representation.
     */
    function divDecimal(uint256 x, uint256 y) internal pure returns (uint256 z) {
        return (x * UNIT) / y;
    }

    /**
     * @dev Reduces scaling for a high precision decimal into a low precision decimal.
     */
    function toLowPrecision(uint256 x) internal pure returns (uint256 z) {
        return x / 1e9; // 9 = 27 - 18
    }

    // -----------------
    // int256
    // -----------------

    /**
     * @dev See mulDecimal for uint256.
     */
    function mulDecimal(int256 x, int256 y) internal pure returns (int256) {
        return (x * y) / UNIT_INT;
    }

    /**
     * @dev See divDecimal for uint256.
     */
    function divDecimal(int256 x, int256 y) internal pure returns (int256) {
        return (x * UNIT_INT) / y;
    }

    /**
     * @dev See toLowPrecision for uint256.
     */
    function toLowPrecision(int256 x) internal pure returns (int256 z) {
        return x / 1e9;
    }

    // -----------------
    // int128
    // -----------------

    // Note: Overloading doesn't seem to work for similar types, i.e. int256 and int128, so explicitly naming the functions differently here.

    /**
     * @dev See mulDecimal for uint256.
     */
    function mulDecimalInt128(int128 x, int128 y) internal pure returns (int128) {
        return (x * y) / UNIT_INT128;
    }

    /**
     * @dev See divDecimal for uint256.
     */
    function divDecimalInt128(int128 x, int128 y) internal pure returns (int128) {
        return (x * UNIT_INT128) / y;
    }

    /**
     * @dev See toLowPrecision for uint256.
     */
    function toLowPrecisionInt128(int128 x) internal pure returns (int128 z) {
        return x / 1e9;
    }
}
